PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX ex: <http://example.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX ssz-schema: <http://ld.stadt-zuerich.ch/schema/>
PREFIX ssz-dimension: <http://ld.stadt-zuerich.ch/statistics/property/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

INSERT {
  
  ?shapeUri a sh:NodeShape ;
    owl:sameAs ?shapeApiUri ;
	sh:targetNode ?target ;
    # alle Ausprägungen einer Dimension
  	sh:property ?b_property .
  
    ?b_property sh:in ?propertyValue  .
 
    # min/max von Literals

    ?b_property sh:minInclusive ?min ;
        sh:maxInclusive ?max .

    # all the list fun
    ?b_property sh:path ?b_list .
    
    ?b_list rdf:first qb:observation ;
        rdf:rest ?b_nil .

    ?b_nil rdf:first ?property ;
        rdf:rest rdf:nil .

} WHERE {

?dataset a qb:DataSet ;
    skos:notation ?datasetNotation ;
    qb:structure/qb:component/(qb:dimension|qb:measure) ?property .

BIND(URI(REPLACE("http://ld.stadt-zuerich.ch/statistics/dataset/%%DATASET%%/shape", "%%DATASET%%" , ?datasetNotation)) AS ?shapeUri)
BIND(URI(REPLACE("http://stat.stadt-zuerich.ch/dataset/%%DATASET%%/shape", "%%DATASET%%" , ?datasetNotation)) AS ?shapeApiUri)          
BIND(URI(REPLACE("http://stat.stadt-zuerich.ch/dataset/%%DATASET%%/slice", "%%DATASET%%" , ?datasetNotation)) AS ?target)


BIND(BNODE() AS ?b_property)
# Dimensionen alleine, für den Pfad
BIND(BNODE() AS ?b_list)
BIND(BNODE() AS ?b_nil)

{
    # Alle Ausprägungen einer Dimension
    SELECT DISTINCT ?dataset ?property ?propertyValue WHERE {
  
        ?obs a qb:Observation ;
            qb:dataSet ?dataset ;
            ?property ?propertyValue .

        FILTER(isIri(?propertyValue))
    }

}
UNION
{
    # Alle Dimensionen mit Literals, nicht URIs
    SELECT ?property ?dataset (MIN(?propertyValue) AS ?min) (MAX(?propertyValue) AS ?max) WHERE {
  
        ?obs a qb:Observation ;
            qb:dataSet ?dataset ;
            ?property ?propertyValue .

        FILTER(isLiteral(?propertyValue))

    } GROUP BY ?property ?dataset

}

}
